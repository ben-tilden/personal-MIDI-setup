# sample ext

IF LOAD
  # see 2.
  ALIAS I0 padMode
  ASS padMode = 0
  # see 4.
  ASS K0 = 1C # remote control event
  ASS L0 = 64 +P # current fixed velocity
  SET LB0 SVEL # set labels to current velocity
  SET LB1 L0 +D
END

# 1. block interfering MIDI
# for channels 1-9 and 13-16
# block all controllers except scene change, stop/start, and velocity

B0-8 0-1B = XX +B
B0-8 1D-67 = XX +B
B0-8 6A-6B = XX +B
B0-8 6E-7F = XX +B
BC-F 0-1B = XX +B
BC-F 1D-67 = XX +B
BC-F 6A-6B = XX +B
BC-F 6E-7F = XX +B
N0-8 = XX +B
NC-F = XX +B

# 2. switch MIDI for sample

# sysex reception
# TODO send 0 on init

IF M0 == F0
  IF M2 == 0C
    ASS padMode = M3
    # TEST
    # ensure sampleext receives padMode
    LOG padMode_SYSEX_RECEIVED padMode
    # END TEST
    BLOCK
    EXIT
  END
END

# If padMode is zero, block all of channel 10
IF padMode == 0
  N9 = XX +B
  B9 0-1B = XX +B
  B9 1D-67 = XX +B
  B9 6A-6B = XX +B
  B9 6E-7F = XX +B
END

# If padMode is one, block all of channel 10 except 24 - 2C and 30
IF padMode == 1
  N9 00-23 = XX XX +B
  N9 2D-2F = XX XX +B
  N9 31-7F = XX XX +B
END

# 3. mapping individual notes
N9 28 = XA 3C # remap 40 to 60 for channel 10 volca sample pt 1
N9 29 = XA 3D # remap 41 to 61 for channel 10 volca sample pt 2
N9 2A = XA 3E # remap 42 to 62 for channel 10 volca sample pt 3
N9 2B = XA 3F # remap 43 to 63 for channel 10 volca sample pt 4
N9 30 = XA 40 # remap 48 to 64 for channel 10 volca sample pt 5
N9 24 = XA 41 # remap 36 to 65 for channel 10 volca sample pt 6
N9 25 = XA 42 # remap 37 to 66 for channel 10 volca sample pt 7
N9 26 = XA 43 # remap 38 to 67 for channel 10 volca sample pt 8
N9 27 = XA 44 # remap 39 to 68 for channel 10 volca sample pt 9
N9 2C = XA 45 # remap 44 to 69 for channel 10 volca sample pt 10

BX 6C 7F = FA # map start control
BX 6D 7F = FC # map stop control

# 4. velocity fixer (remote controllable)
# if velocity becomes a consistent issue, add scene for non-fixed velocity

# handle remote control of velocity
IF MT == B0
  IF M1 == K0
    ASS L0 = M2 # assign velocity
    SET LB1 L0 +D # show current velocity on label
  END
END

# fix velocity
IF MT == 90
  IF M2 > 0
    ASS M2 = L0
  END
END
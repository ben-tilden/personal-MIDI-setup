# mapping filter

# NOTE this is not changed on init due to routing / filter issues - should only matter for velocity anyways
IF LOAD
  # see 6.
  ASS K0 = 7F # remote control event
  SET Q1 VELOCITY
  ASS Q1 = 64 # current fixed velocity
END

# 4. mapping individual notes
# seemingly out of order due to StreamByter being very literal with the way it assigns
# CC's need to be remapped before they are mapped again

N9 32 = X1 3C # remap 50 to 62 for channel 10 volca drum pt 2
N9 2B = X9 32 # remap 43 to 50 for channel 10 hi tom
N9 2A = X9 2B # remap 42 to 43 for channel 10 lo tom
N9 24 = X9 2A # remap 36 to 40 for channel 10 cl hat
N9 28 = X9 24 # remap 40 to 36 for channel 10 bass
N9 26 = X9 4B # remap 38 to 75 for channel 10 claves
N9 29 = X9 26 # remap 41 to 38 for channel 10 snare
N9 2E = X4 3C # remap 46 to 67 for channel 10 volca drum pt 5
N9 25 = X9 2E # remap 37 to 46 for channel 10 op hat
N9 27 = X9 43 # remap 39 to 67 for channel 10 agogo
N9 30 = X9 27 # remap 48 to 39 for channel 10 clap
N9 31 = X0 3C # remap 49 to 60 for channel 10 volca drum pt 1
N9 2C = X9 31 # remap 44 to 49 for channel 10 crash
N9 33 = X2 3C # remap 51 to 64 for channel 10 volca drum pt 3
N9 2D = X3 3C # remap 45 to 65 for channel 10 volca drum pt 4
N9 2F = X5 3C # remap 47 to 69 for channel 10 volca drum pt 6

# 5. mapping individual controls

BX 6C 7F = FA # map start control
BX 6D 7F = FC # map stop control

# mapping drum

# mapping hidden MIDI for bass
B7 15 = B7 0B # expression to knob 1
B7 16 = B7 31 # gate time to knob 2
B7 17 = B7 05 # slide time to knob 3
B7 18 = B7 28 # octave to knob 4

# 6. velocity fixer (remote controllable)
# if velocity becomes a consistent issue, add scene for non-fixed velocity

# deassign knob 8 to free it up for velocity
IF MT == B0
  IF M1 == 1C
    ASS M1 = K0
  END
END

# handle remote control of velocity
IF MT == B0
  IF M1 == K0
    # this causes issues with launchkey if velocity is zero, since it registers as a note off
    IF M2 == 0
      ASS M2 = 1
    END
    ASS Q1 = M2 # assign velocity
  END
END

# fix velocity
IF MT == 90
  IF M2 > 0
    ASS M2 = Q1
  END
END

# channel 8 hardware filter
# NOTE this is not changed on init due to routing / filter complications - should only impact velocity

IF LOAD
  # constant
  ALIAS 07 thisChannel

  # see 6.
  SET Q1 VELOCITY
  ASS Q1 = 64 # current fixed velocity
  
END

# uncomment if virtual ports do not work
# block if a message meant for xtouch
#IF MT == B0
#  IF M1 < 13
#    BLOCK
#    EXIT
#  END
#END

IF MC != thisChannel
  IF M1 != 68 7F
    # commenting these out because the intention at this point is to use modstep as the sequencer - this would start hardware sequencers along with modstep sequencers
    # can change later if desired
    #IF M0 != FA
      #IF M0 != FC
        BLOCK
        EXIT
      #END
    #END
  END
END

# mapping bass (8 channel specific)

# mapping hidden MIDI for bass
B7 15 = B7 0B # expression to knob 1
B7 16 = B7 31 # gate time to knob 2
B7 17 = B7 05 # slide time to knob 3
B7 18 = B7 28 # octave to knob 4

# 6. velocity fixer (remote controllable)
# if velocity becomes a consistent issue, add scene for non-fixed velocity

# deassign knob 8 to free it up for velocity
IF MT == B0
  IF M1 == 1C
    ASS M1 = 7F
  END
END

# handle remote control of velocity
IF MT == B0
  IF M1 == 7F
    # this causes issues with launchkey if velocity is zero, since it registers as a note off
    IF M2 == 0
      ASS M2 = 1
    END
    ASS Q1 = M2 # assign velocity
  END
END

# fix velocity
IF MT == 90
  IF M2 > 0
    ASS M2 = Q1
  END
END

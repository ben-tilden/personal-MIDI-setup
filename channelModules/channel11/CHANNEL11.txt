// channel eleven

@OnLoad

  // minimal layout
  ShowLayout 4

  // constant

  channel = 10 // channel specific (obviously)

  phraseSysexOffsetConstant = 42 // NOTE artifact of initial SB setup - could be cleaned up

  // variable

  clockNum = 0

  arpMode = 0
  modFactor = 1

@End

// sysex reception

@OnSysex

  ReceiveSysex sysexData

  // ALLOW CC
  // BLOCK 11 0F 01 FF 3F 9F (+ xtouch program change, key presses, key releases, and CC's)
  if sysexData[0] <> 0xCC and sysexData[0] <> 0xC9
    Exit

  // clock message triggered by host clock starting
  elseif sysexData[0] = 0x7D and sysexData[1] = 0x01
    Exit

  elseif sysexData[1] = 0x00
    numPressed = sysexData[2]
    if numPressed = 0
      // NOTE clockNum used to be reset by gateOpened, but was causing issues of multiple notes getting fired milliseconds apart (and overlapping in an awkward way)
      clockNum = 0
    endif
    Exit

  elseif sysexData[1] = 0x01
    arpPhraseToggle = sysexData[2]
    Exit

  elseif sysexData[1] = 0x02
    arpPhraseOnOffToggle = sysexData[2]
    Exit

  elseif sysexData[1] = 0x03
    tempoPulse = sysexData[2]
    Exit

  elseif sysexData[1] = 0x04
    gate = sysexData[2] * 256 + sysexData[3] // 0x100
    Exit

  elseif sysexData[1] = 0x05
    velocity = sysexData[2]
    Exit

  elseif sysexData[1] = 0x06
    arpValueModeMod = sysexData[2]

    if arpValueModeMod = 0 or arpValueModeMod = 1 or arpValueModeMod = 2
      arpMode = 0 // asc
    elseif arpValueModeMod = 3 or arpValueModeMod = 4 or arpValueModeMod = 5
      arpMode = 1 // desc
    elseif arpValueModeMod = 6 or arpValueModeMod = 7 or arpValueModeMod = 8
      arpMode = 2 // rand
    elseif arpValueModeMod = 9 or arpValueModeMod = 10 or arpValueModeMod = 11
      arpMode = 3 // ord
    endif

    if arpMode = 0 or arpMode = 3 // asc, order
      initIndex = numPressed - 1 
      endIndex = 0
      iterConstant = -1
      // NOTE commenting this out likely can cause issues if arpValues are changed at a similar time to arpMode
      //arpIndex = initIndex
      octaveOffset = 0
    elseif arpMode = 1 // desc
      initIndex = 0
      endIndex = numPressed - 1
      iterConstant = 1
      // NOTE commenting this out likely can cause issues if arpValues are changed at a similar time to arpMode
      //arpIndex = initIndex
      octaveOffset = 0
    endif

    if arpValueModeMod = 0 or arpValueModeMod = 3 or arpValueModeMod = 6 or arpValueModeMod = 9
      modFactor = 1
    elseif arpValueModeMod = 1 or arpValueModeMod = 4 or arpValueModeMod = 7 or arpValueModeMod = 10
      modFactor = 2
    elseif arpValueModeMod = 2 or arpValueModeMod = 5 or arpValueModeMod = 8 or arpValueModeMod = 11
      modFactor = 3
    endif

    Exit

  elseif sysexData[1] = 0x08
    phraseVal = sysexData[2]
    Exit

  elseif sysexData[1] = 0x0A
    phraseLength = sysexData[2]
    Exit

  elseif sysexData[1] = 0x19
    gateOpened = 1
    Exit

  elseif sysexData[1] = 0x20
    for i = 2 to 11
      arpValues[i - 2] = sysexData[i]
    endfor
    Exit

  elseif sysexData[1] > 41 and sysexData[1] < 72
    // reset phraseValues if we're restarting
    if sysexData[1] = 42
      for i = 0 to 31
        phraseOffsetValues[i] = 0
      endfor
    endif
    setPhraseIndex = sysexData[1] - phraseSysexOffsetConstant // artifact of initial SB setup - could be changed
    if sysexData[2] <= 128
      phraseOffsetValues[setPhraseIndex] = sysexData[2]
    elseif sysexData[2] > 128
      phraseOffsetValues[setPhraseIndex] = sysexData[2] + -256 // negative value
    endif

    Exit

  elseif sysexData[1] = 0x4A
    arpMute = sysexData[2]
    Exit

  elseif sysexData[1] = 0x4C
    // infiniteGate used for two main uses cases:
    // 1. prevent MIDI note-offs from restricting the highest possible tempo for synths that can be interrupted by note-offs
    // 2. ensure that whenever arp or phrase with latch is converted to just latch, note-offs do not interrupt full note transition
    infiniteGate = sysexData[2]
    Exit

  // reset channel when init sysex received
  elseif sysexData[1] = 0x1C and sysexData[2] = 0x00

    clockNum = 0

    arpMode = 0
    modFactor = 1

    // (re)set sysex variables
    // NOTE could move this also into the init load for completeness (or remove local from init load?)

    numPressed = 0
    arpPhraseToggle = 0
    arpPhraseOnOffToggle = 0
    tempoPulse = 36 // 0x24
    infiniteGate = 0
    gate = 0
    velocity = 100 // 0x64
    arpValueModeMod = 0
    phraseVal = 0
    phraseLength = 0
    gateOpened = 0
    arpMute = 0
    for i = 0 to 9
      arpValues[i] = 0
    endfor
    for i = 0 to 31
      phraseOffsetValues[i] = 0
    endfor

    // reset necessary local variables
    clockNum = 102500

  endif

@End

// end sysex reception

@HandleClockRandom
  octaveOffset = random 0, modFactor - 1
  arpIndex = random 0, numPressed - 1
  arpValSend = arpValues[arpIndex] + octaveOffset * 12
  SendMIDINoteOn channel, arpValSend, velocity
  if infiniteGate = 0
    SendMIDINoteOff channel, arpValSend, velocity, gate
  endif
@End

@HandleClockPattern
  arpValSend = arpValues[arpIndex] + octaveOffset * 12 * -1 * iterConstant
  SendMIDINoteOn channel, arpValSend, velocity
  if infiniteGate = 0
    SendMIDINoteOff channel, arpValSend, velocity, gate
  endif
  if arpIndex = endIndex
    arpIndex = initIndex
    octaveOffset = (octaveOffset + 1) % modFactor
  else
    arpIndex = arpIndex + iterConstant
  endif
@End

// uncomment to change to old sequence for clock patterns
//@HandleClockPattern
//  octaveOffset = (octaveOffset + 1) % modFactor
//  arpValSend = arpValues[arpIndex] + octaveOffset * 12
//  SendMIDINoteOn channel, arpValSend, velocity
//  if infiniteGate = 0
//    SendMIDINoteOff channel, arpValSend, velocity, gate
//  endif
//  if octaveOffset = modFactor - 1 // NOTE a couple of ways to do this
//    if arpIndex = endIndex
//      arpIndex = initIndex
//    else
//      arpIndex = arpIndex + iterConstant
//    endif
//  endif
//@End

@OnMIDIInput
  if arpPhraseOnOffToggle = 1 and numPressed > 0 and MIDIByte1 = 0xF8
    if gateOpened = 1
      arpMute = 0
    endif

    if clockNum % tempoPulse = 0 // note sent - allow clock
      // if isArp
      if arpPhraseToggle = 0 and arpMute = 0

        // if gateOpened, or if first instance of clock since first key was pressed
        if gateOpened = 1 // NOTE could make handler out of the following, since used twice
          if arpMode = 0 or arpMode = 3 // asc, order
            initIndex = numPressed - 1 
            endIndex = 0
            iterConstant = -1
            arpIndex = initIndex
            octaveOffset = 0
          elseif arpMode = 1 // desc
            initIndex = 0
            endIndex = numPressed - 1
            iterConstant = 1
            arpIndex = initIndex
            octaveOffset = 0
          endif
          gateOpened = 0
        endif

        if arpMode = 2 // rand
          Call @HandleClockRandom
        else
          Call @HandleClockPattern
        endif

      // else isPhrase
      // because we need the phrase to be relative, we'll need the global array to contain offset, not note value
      elseif arpPhraseToggle = 1 and arpMute = 0
        // if gateOpened, or if first instance of clock since first key was pressed
        if gateOpened = 1
          phraseIndex = 0
          gateOpened = 0
        endif
        // if phrase offset is not a rest
        if phraseOffsetValues[phraseIndex] <> 128
          phraseValSend = phraseVal + phraseOffsetValues[phraseIndex]
          SendMIDINoteOn channel, phraseValSend, velocity
          if infiniteGate = 0
            SendMIDINoteOff channel, phraseValSend, velocity, gate
          endif
        endif
        if phraseIndex <> phraseLength // NOTE this is really phraseLength - 1
          phraseIndex = phraseIndex + 1
        else
          phraseIndex = 0
        endif
      endif
    endif
    clockNum = clockNum + 1
  endif

@End
